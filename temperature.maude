in ~/Documents/Maude_Metacontrol/actuators.maude

mod TEMPERATURE is
  protecting ACTUATORS .
  protecting FLOAT .

  sorts Thermometer .
  subsorts Thermometer < Configuration .

  sorts Temperature .
  subsort Float < Temperature .

  op < _ :Thermometer | Degrees: _ > : Oid Temperature -> Thermometer [format (ntb b b b b b b o)] .

  op cold? : Temperature -> Bool .
  op hot? : Temperature -> Bool .

  var DG : Temperature .

  ceq cold?(DG) = true if DG < 18.0 .
  ceq cold?(DG) = false if DG >= 18.0 .

  ceq hot?(DG) = true if DG > 22.0 .
  ceq hot?(DG) = false if DG <= 22.0 .


  vars H WH W T C : Oid .
  var HS : HeaterStatus .
  var WHS : WaterheaterStatus .
  var WS : WindowStatus .
  vars RAT RAT1 RAT2 : RuleAppliedTemp .
  vars RAAQ RAAQ1 RAAQ2 : RuleAppliedAQ .
  vars ET ET1 ET2 : EffectTemp .
  vars EAQ EAQ1 EAQ2 : EffectAQ .
  vars B B1 B2 : Broken .

  *** Rules heater
  *** If the heater did not affect the temperature in the room in the current hour yet, then the temperature increases depending on the status of the heater.
  rl [TempHeaterOff] :
    < H :Heater | Status: hOff, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < H :Heater | Status: hOff, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    [print "Degrees: " DG ", rule: [TempHeaterOff]"] .

  rl [TempHeaterFairlyhot] :
    < H :Heater | Status: fairlyhot, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < H :Heater | Status: fairlyhot, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG + ET >
    [print "Degrees: " DG ", rule: [TempHeaterFairlyhot]"] .

  rl [TempHeaterVeryhot] :
    < H :Heater | Status: veryhot, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < H :Heater | Status: veryhot, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG + 2.0 * ET >
    [print "Degrees: " DG ", rule: [TempHeaterVeryhot] "] .

  *** Rules water heater
  *** If the water heater did not affect the temperature in the room in the current hour yet, then the temperature increases depending on the status of the water heater.
  rl [TempWaterheaterOff] :
    < WH :Waterheater | Status: whOff, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < WH :Waterheater | Status: whOff, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    [print "Degrees: " DG ", rule: [TempWaterheaterOff]"] .

  rl [TempWaterheaterOn] :
    < WH :Waterheater | Status: on, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < WH :Waterheater | Status: on, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG + ET >
    [print "Degrees: " DG ", rule: [TempWaterheaterOn] "] .

  *** Rules Window
  *** If the window did not affect the temperature in the room in the current hour yet, then the temperature decreases depending on the status of the window.
  rl [TempWindowClosed] :
    < W :Window | Status: closed, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < W :Window | Status: closed, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    [print "Degrees: " DG ", rule: [TempWindowClosed]"] .

  rl [TempWindowHalfopen] :
    < W :Window | Status: halfopen, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < W :Window | Status: halfopen, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG + ET >
    [print "Degrees: " DG "rule: [TempWindowHalfopen]"] .

  rl [TempWindowOpen] :
    < W :Window | Status: open, RuleAppliedTemp: no, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG >
    =>
    < W :Window | Status: open, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < T :Thermometer | Degrees: DG + 2.0 * ET >
    [print "Degrees: " DG ", rule: [TempWindowOpen]"] .

  *** If heater and water heater are off and the window is closed, then the temperature also decreases a bit
  rl [TempAllOff] :
    < H :Heater | Status: hOff, RuleAppliedTemp: RAT, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B >
    < WH :Waterheater | Status: whOff, RuleAppliedTemp: RAT1, RuleAppliedAQ: RAAQ1, EffectsTemp: ET1, EffectsAQ: EAQ1, Broken: B1 >
    < W :Window | Status: closed, RuleAppliedTemp: RAT2, RuleAppliedAQ: RAAQ2, EffectsTemp: ET2, EffectsAQ: EAQ2, Broken: B2 >
    < T :Thermometer | Degrees: DG >
    =>
    < H :Heater | Status: hOff, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B >
    < WH :Waterheater | Status: whOff, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ1, EffectsTemp: ET1, EffectsAQ: EAQ1, Broken: B1 >
    < W :Window | Status: closed, RuleAppliedTemp: yes, RuleAppliedAQ: RAAQ2, EffectsTemp: ET2, EffectsAQ: EAQ2, Broken: B2 >
    < T :Thermometer | Degrees: DG - 0.25 >
    [print "Degrees: " DG ", rule: [TempAllOff]"] .

endm
