mod AIRQUALITY is
  protecting ACTUATORS .

  sort Airquality .
  subsort Airquality < Configuration .

  sort AirqualityStatus .
  subsort Float < AirqualityStatus .        *** 0 Airquality is normal, airquality >0 is good, airquality < 0 is bad

  op < _ :Airquality | Value: _ > : Oid AirqualityStatus -> Airquality [format (ntb b b b b b b o)] .

  op aqok? : AirqualityStatus -> Bool .
  op getAirquality : Configuration -> AirqualityStatus .

  var AQ : Oid .
  var AQS : AirqualityStatus .
  var CON : Configuration .

  ceq aqok?(AQS) = true if AQS >= 0.0 .
  ceq aqok?(AQS) = false if AQS < 0.0 .

  eq getAirquality(< AQ :Airquality | Value: AQS > CON) = AQS .

  vars H WH W : Oid .
  var HS : HeaterStatus .
  var WHS : WaterheaterStatus .
  var WS : WindowStatus .
  var RAT : RuleAppliedTemp .
  var ET : EffectTemp .
  var EAQ : EffectAQ .
  var B : Broken .

  *** Rules heater
  *** If the heater did not affect the air quality in the room in the current hour yet, then the air quality decreases depending on the status of the heater.
  rl [AQHeaterOff] :
    < H :Heater | Status: off, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < H :Heater | Status: off, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    [print "Air quality: " AQS ", rule: [AQHeaterOff]"] .

  rl [AQHeaterFairlyhot] :
    < H :Heater | Status: fairlyhot, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < H :Heater | Status: fairlyhot, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS + EAQ >
    [print "Air quality: " AQS ", rule: [AQHeaterFairlyhot]"] .

  rl [AQHeaterVeryhot] :
    < H :Heater | Status: veryhot, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < H :Heater | Status: veryhot, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS + 2.0 * EAQ >
    [print "Air quality: " AQS ", rule: [AQHeaterVeryhot]"] .

  *** Rules water heater
  *** If the water heater did not affect the air quality in the room in the current hour yet, then the air quality decreases depending on the status of the water heater.
  rl [AQWaterheaterOff] :
    < WH :Waterheater | Status: off, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < WH :Waterheater | Status: off, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    [print "Air quality: " AQS ", rule: [AQWaterheaterOff]"] .

  rl [AQWaterheaterOn] :
    < WH :Waterheater | Status: on, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < WH :Waterheater | Status: on, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS + 2.0 * EAQ >
    [print "Air quality: " AQS ", rule: [AQWaterheaterOn]"] .

  *** Rules Window
  *** If the window did not affect the air quality in the room in the current hour yet, then the air quality increases depending on the status of the window.
  rl [AQWindowClosed] :
    < W :Window | Status: closed, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < W :Window | Status: closed, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    [print "Air quality: " AQS ", rule: [AQWindowClosed]"] .

  rl [AQWindowHalfopen] :
    < W :Window | Status: halfopen, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < W :Window | Status: halfopen, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS + EAQ >
    [print "Air quality: " AQS "rule: [AQWindowHalfopen]"] .

  rl [AQWindowOpen] :
    < W :Window | Status: open, RuleAppliedTemp: RAT, RuleAppliedAQ: no, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS >
    =>
    < W :Window | Status: open, RuleAppliedTemp: RAT, RuleAppliedAQ: yes, EffectsTemp: ET, EffectsAQ: EAQ, Broken: B > < AQ :Airquality | Value: AQS + 2.0 * EAQ >
    [print "Air quality: " AQS ", rule: [AQWindowOpen]"] .

endm
