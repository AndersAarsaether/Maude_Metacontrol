in comfort_controller.maude
in eco_controller.maude
in heater_err_controller.maude
in wh_error_controller.maude
in window_error_controller.maude
in qualityattributes.maude

mod METACONTROL is
  protecting COMFORTCONTROLLER .
  protecting ECOCONTROLLER .
  protecting HEATERERRCONTROLLER .
  protecting WATERHEATERERRCONTROLLER .
  protecting WINDOWERRCONTROLLER .
  protecting QUALITYATTRIBUTES .


  sort Metacontroller .
  subsort Metacontroller < Configuration .

  op < _ :Metacontroller > : Oid -> Metacontroller [format (ntr r r r o)] .

  vars H WH W CC EC HEC WHEC WEC MC QAC QAAQ : Oid .
  var HS : HeaterStatus .
  var WHS : WaterheaterStatus .
  var WS : WindowStatus .
  var A : Attribute .
  var N : Nat .
  var QAL : QaList .
  var QAS : QaStatus .
  var QACOM : QAComputed .
  var CONF : Configuration .
  var SC : ScheduleComponents .
  vars B B1 B2 B3 : Bool .


  *** If no metacontroller is in initial configuration, we want to have a dummy rule s.t. we can test without metacontroller
  op noMetacontr : Configuration -> Bool .
  eq noMetacontr(< MC :Metacontroller > CONF) = false .
  eq noMetacontr(CONF) = true [owise] .


  ******** Checks from comfort to other controller
  *** Check whether metacontroller should change from comfort to heater error controller
  op metaComfToHErr : Configuration -> Bool .
  eq metaComfToHErr(< H :Heater | Broken: no, A > CONF) = false and metaComfToHErr(CONF) .
  eq metaComfToHErr(< CC :ComfortController | Selected: false > CONF) = false and metaComfToHErr(CONF) .
  eq metaComfToHErr(< HEC :HeaterErrController | Selected: true > CONF) = false and metaComfToHErr(CONF) .
  ceq metaComfToHErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaComfToHErr(CONF)
    if SC =/= MCChange .
  eq metaComfToHErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from comfort to water heater error controller
  op metaComfToWhErr : Configuration -> Bool .
  eq metaComfToWhErr(< WH :Waterheater | Broken: no, A > CONF) = false and metaComfToWhErr(CONF) .
  eq metaComfToWhErr(< CC :ComfortController | Selected: false > CONF) = false and metaComfToWhErr(CONF) .
  eq metaComfToWhErr(< WHEC :WaterheaterErrController | Selected: true > CONF) = false and metaComfToWhErr(CONF) .
  ceq metaComfToWhErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaComfToWhErr(CONF)
    if SC =/= MCChange .
  eq metaComfToWhErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from comfort to window error controller
  op metaComfToWErr : Configuration -> Bool .
  eq metaComfToWErr(< W :Window | Broken: no, A > CONF) = false and metaComfToWErr(CONF) .
  eq metaComfToWErr(< CC :ComfortController | Selected: false > CONF) = false and metaComfToWErr(CONF) .
  eq metaComfToWErr(< WEC :WindowErrController | Selected: true > CONF) = false and metaComfToWErr(CONF) .
  ceq metaComfToWErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaComfToWErr(CONF)
    if SC =/= MCChange .
  eq metaComfToWErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from comfort to eco controller
  op metaComfToEco : Configuration -> Bool .
  eq metaComfToEco(< CC :ComfortController | Selected: false > CONF) = false and metaComfToEco(CONF) .
  eq metaComfToEco(< EC :EcoController | Selected: true > CONF) = false and metaComfToEco(CONF) .
  ceq metaComfToEco(< QAAQ :QaAirquality | Consider: N, Past: QAL, Status: QAS, QAComputed: QACOM > CONF) = false and metaComfToEco(CONF)
    if QAS >= 6/10 .
  ceq metaComfToEco(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaComfToEco(CONF)
    if SC =/= MCChange .
  eq metaComfToEco(CONF) = true [owise] .


  ****** Check from eco to other controller
  *** Check whether metacontroller should change from eco to window error controller
  op metaEcoToHErr : Configuration -> Bool .
  eq metaEcoToHErr(< H :Heater | Broken: no, A > CONF) = false and metaEcoToHErr(CONF) .
  eq metaEcoToHErr(< EC :EcoController | Selected: false > CONF) = false and metaEcoToHErr(CONF) .
  eq metaEcoToHErr(< HEC :HeaterErrController | Selected: true > CONF) = false and metaEcoToHErr(CONF) .
  ceq metaEcoToHErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaEcoToHErr(CONF)
    if SC =/= MCChange .
  eq metaEcoToHErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from eco to window error controller
  op metaEcoToWhErr : Configuration -> Bool .
  eq metaEcoToWhErr(< WH :Waterheater | Broken: no, A > CONF) = false and metaEcoToWhErr(CONF) .
  eq metaEcoToWhErr(< EC :EcoController | Selected: false > CONF) = false and metaEcoToWhErr(CONF) .
  eq metaEcoToWhErr(< HEC :WaterheaterErrController | Selected: true > CONF) = false and metaEcoToWhErr(CONF) .
  ceq metaEcoToWhErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaEcoToWhErr(CONF)
    if SC =/= MCChange .
  eq metaEcoToWhErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from eco to window error controller
  op metaEcoToWErr : Configuration -> Bool .
  eq metaEcoToWErr(< W :Window | Broken: no, A > CONF) = false and metaEcoToWErr(CONF) .
  eq metaEcoToWErr(< EC :EcoController | Selected: false > CONF) = false and metaEcoToWErr(CONF) .
  eq metaEcoToWErr(< HEC :WindowErrController | Selected: true > CONF) = false and metaEcoToWErr(CONF) .
  ceq metaEcoToWErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaEcoToWErr(CONF)
    if SC =/= MCChange .
  eq metaEcoToWErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from eco to comfort controller
  op metaEcoToComf : Configuration -> Bool .
  eq metaEcoToComf(< EC :EcoController | Selected: false > CONF) = false and metaEcoToComf(CONF) .
  eq metaEcoToComf(< CC :ComfortController | Selected: true > CONF) = false and metaEcoToComf(CONF) .
  ceq metaEcoToComf(< QAC :QaComfort | Consider: N, Past: QAL, Status: QAS, QAComputed: QACOM > CONF) = false and metaEcoToComf(CONF)
    if QAS >= 6/10 .
  ceq metaEcoToComf(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false and metaEcoToComf(CONF)
    if SC =/= MCChange .
  eq metaEcoToComf(CONF) = true [owise] .

  *** Check if at least one rule is applicable
  op metaRuleApplicable : Configuration -> Bool .
  eq metaRuleApplicable(CONF) = metaComfToHErr(CONF) or metaComfToWhErr(CONF) or metaComfToWErr(CONF) or metaComfToEco(CONF)
                                or metaEcoToHErr(CONF) or metaEcoToWhErr(CONF) or metaEcoToWErr(CONF) or metaEcoToComf(CONF) .



*** TODO Should the metacontroller also turn the actuators off?
  ****************** Rules
  ****************************************** From comfort controller to error controller
  *** If the comfort controller is active and the heater is broken, then activate the heater error controller
  rl [MetaContrComfortToHErr] :
    < H :Heater | Broken: yes, A >
    < CC :ComfortController | Selected: true >
    < HEC :HeaterErrController | Selected: false >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < H :Heater | Broken: yes, A >
    < CC :ComfortController | Selected: false >
    < HEC :HeaterErrController | Selected: true >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrComfortToHErr]"] .

  *** If the comfort controller is active and the water heater is broken, then activate the water heater error controller
  rl [MetaContrComfortToWhErr] :
    < WH :Waterheater | Broken: yes, A >
    < CC :ComfortController | Selected: true >
    < WHEC :WaterheaterErrController | Selected: false >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < WH :Waterheater | Broken: yes, A >
    < CC :ComfortController | Selected: false >
    < WHEC :WaterheaterErrController | Selected: true >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrComfortToWhErr]"] .

  *** If the comfort controller is active and the window is broken, then activate the window error controller
  rl [MetaContrComfortToWErr] :
    < W :Window | Broken: yes, A >
    < CC :ComfortController | Selected: true >
    < WEC :WindowErrController | Selected: false >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < W :Window | Broken: yes, A >
    < CC :ComfortController | Selected: false >
    < WEC :WindowErrController | Selected: true >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrComfortToWErr]"] .



  ******************************************* From eco controller to error controller
  *** If the eco controller is active and the heater is broken, then activate the heater error controller
  rl [MetaContrEcoToHErr] :
    < H :Heater | Broken: yes, A >
    < EC :EcoController | Selected: true >
    < HEC :HeaterErrController | Selected: false >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < H :Heater | Broken: yes, A >
    < EC :EcoController | Selected: false >
    < HEC :HeaterErrController | Selected: true >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrEcoToHErr]"] .

  *** If the eco controller is active and the water heater is broken, then activate the water heater error controller
  rl [MetaContrEcoToWhErr] :
    < WH :Waterheater | Broken: yes, A >
    < EC :EcoController | Selected: true >
    < WHEC :WaterheaterErrController | Selected: false >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < WH :Waterheater | Broken: yes, A >
    < EC :EcoController | Selected: false >
    < WHEC :WaterheaterErrController | Selected: true >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrEcoToWhErr]"] .

  *** If the eco controller is active and the window is broken, then activate the window error controller
  rl [MetaContrEcoToWErr] :
    < W :Window | Broken: yes, A >
    < EC :EcoController | Selected: true >
    < WEC :WindowErrController | Selected: false >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < W :Window | Broken: yes, A >
    < EC :EcoController | Selected: false >
    < WEC :WindowErrController | Selected: true >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrEcoToWErr]"] .




  ************************************** Change between comfort and eco controller
  *** If QA comfort is below 0.6 and eco controller is active, then change to comfort controller
  crl [MetaContrEcoToComf] :
    < CC :ComfortController | Selected: false >
    < EC :EcoController | Selected: true >
    < QAC :QaComfort | Consider: N, Past: QAL, Status: QAS, QAComputed: QACOM >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < CC :ComfortController | Selected: true >
    < EC :EcoController | Selected: false >
    < QAC :QaComfort | Consider: N, Past: QAL, Status: QAS, QAComputed: QACOM >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    if QAS < 6/10
    [print "rule: [MetaContrEcoToComf]"] .

  *** If QA air quality is below 0.6 and comfort controller is active, then change to eco controller
  crl [MetaContrComfToEco] :
    < CC :ComfortController | Selected: true >
    < EC :EcoController | Selected: false >
    < QAAQ :QaAirquality | Consider: N, Past: QAL, Status: QAS, QAComputed: QACOM >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < CC :ComfortController | Selected: false >
    < EC :EcoController | Selected: true >
    < QAAQ :QaAirquality | Consider: N, Past: QAL, Status: QAS, QAComputed: QACOM >
    < MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    if QAS < 6/10
    [print "rule: [MetaContrComfToEco]"] .



  *************************** Dummy rules
  *** If the metacontroller no other rule can be applied, this will be applied
  crl [MetacontrollerDoesNothing] :
    {< MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 > CONF}
    =>
    {< MC :Metacontroller >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 > CONF}
    if metaRuleApplicable(< MC :Metacontroller > < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 > CONF) == false
    [print "rule: [MetacontrollerDoesNothing]"] .

  *** If there is no metacontroller, this rules ensures that we can run tests anyway
  crl [noMetacontroller] :
    {< Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 > CONF}
    =>
    {< Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 > CONF}
    if noMetacontr(CONF) == true
    [print "rule: [noMetacontr]"] .

endm
eof
