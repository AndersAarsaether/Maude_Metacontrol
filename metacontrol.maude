in comfort_controller.maude
in eco_controller.maude
in heater_err_controller.maude
in wh_error_controller.maude
in window_error_controller.maude
in qualityattributes.maude

mod METACONTROL is
  protecting COMFORTCONTROLLER .
  protecting ECOCONTROLLER .
  protecting HEATERERRCONTROLLER .
  protecting WATERHEATERERRCONTROLLER .
  protecting WINDOWERRCONTROLLER .
  protecting QUALITYATTRIBUTES .


  sort Metacontroller .
  subsort Metacontroller < Configuration .

  sorts ContrSelected MetaPair MetaLog .
  subsort MetaPair < MetaLog .
  ops Eco Comf HErr WhErr WErr : -> ContrSelected [ctor] .
  op (_, _) : Timesteps ContrSelected -> MetaPair [ctor] .
  op nil : -> MetaLog [ctor] .
  op __ : MetaLog MetaLog -> MetaLog [ctor assoc id: nil] .

  op < _ :Metacontroller | MetaLog: _ > : Oid MetaLog -> Metacontroller [format (ntr r r r r r r o)] .

  vars H WH W CC EC HEC WHEC WEC MC QAC QAAQ C : Oid .
  var HS : HeaterStatus .
  var WHS : WaterheaterStatus .
  var WS : WindowStatus .
  var A : Attribute .
  var N : Nat .
  var QAL : List{Bool} .
  var QAS : QaStatus .
  var QACOM : QAComputed .
  var CONF : Configuration .
  var SC : ScheduleComponents .
  vars B B1 B2 B3 : Bool .
  var R : Rat .
  var ML : MetaLog .
  var TS : Timesteps .
  var TI : Time .
  vars TL AL : TVPList .


  ********** Cost function qualtiy attributes
  op cost : Configuration -> Rat .
  eq cost(CONF) = 5/10 * getQaComfValue(CONF) + 3/10 * getQaAqValue(CONF) + 2/10 * getQaEcoValue(CONF) .

  *** If no metacontroller is in initial configuration, we want to have a dummy rule s.t. we can test without metacontroller
  op noMetacontr : Configuration -> Bool .
  eq noMetacontr(< MC :Metacontroller | MetaLog: ML > CONF) = false .
  eq noMetacontr(CONF) = true [owise] .


  ******** Checks from comfort to other controller
  *** Check whether metacontroller should change from comfort to heater error controller
  op metaComfToHErr : Configuration -> Bool .
  eq metaComfToHErr(< H :Heater | Broken: no, A > CONF) = false .
  eq metaComfToHErr(< CC :ComfortController | Selected: false > CONF) = false .
  eq metaComfToHErr(< HEC :HeaterErrController | Selected: true > CONF) = false .
  ceq metaComfToHErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  eq metaComfToHErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from comfort to water heater error controller
  op metaComfToWhErr : Configuration -> Bool .
  eq metaComfToWhErr(< WH :Waterheater | Broken: no, A > CONF) = false .
  eq metaComfToWhErr(< CC :ComfortController | Selected: false > CONF) = false .
  eq metaComfToWhErr(< WHEC :WaterheaterErrController | Selected: true > CONF) = false .
  ceq metaComfToWhErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  eq metaComfToWhErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from comfort to window error controller
  op metaComfToWErr : Configuration -> Bool .
  eq metaComfToWErr(< W :Window | Broken: no, A > CONF) = false .
  eq metaComfToWErr(< CC :ComfortController | Selected: false > CONF) = false .
  eq metaComfToWErr(< WEC :WindowErrController | Selected: true > CONF) = false .
  ceq metaComfToWErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  eq metaComfToWErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from comfort to eco controller
  op metaComfToEco : Configuration -> Bool .
  eq metaComfToEco(< CC :ComfortController | Selected: false > CONF) = false .
  eq metaComfToEco(< EC :EcoController | Selected: true > CONF) = false .
  ceq metaComfToEco(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  ceq metaComfToEco(CONF) = false
    if cost(CONF) <= 7/10 .
  eq metaComfToEco(CONF) = true [owise] .


  ****** Check from eco to other controller
  *** Check whether metacontroller should change from eco to window error controller
  op metaEcoToHErr : Configuration -> Bool .
  eq metaEcoToHErr(< H :Heater | Broken: no, A > CONF) = false .
  eq metaEcoToHErr(< EC :EcoController | Selected: false > CONF) = false .
  eq metaEcoToHErr(< HEC :HeaterErrController | Selected: true > CONF) = false .
  ceq metaEcoToHErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  eq metaEcoToHErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from eco to window error controller
  op metaEcoToWhErr : Configuration -> Bool .
  eq metaEcoToWhErr(< WH :Waterheater | Broken: no, A > CONF) = false .
  eq metaEcoToWhErr(< EC :EcoController | Selected: false > CONF) = false .
  eq metaEcoToWhErr(< HEC :WaterheaterErrController | Selected: true > CONF) = false .
  ceq metaEcoToWhErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  eq metaEcoToWhErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from eco to window error controller
  op metaEcoToWErr : Configuration -> Bool .
  eq metaEcoToWErr(< W :Window | Broken: no, A > CONF) = false .
  eq metaEcoToWErr(< EC :EcoController | Selected: false > CONF) = false .
  eq metaEcoToWErr(< HEC :WindowErrController | Selected: true > CONF) = false .
  ceq metaEcoToWErr(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  eq metaEcoToWErr(CONF) = true [owise] .

  *** Check whether metacontroller should change from eco to comfort controller
  op metaEcoToComf : Configuration -> Bool .
  eq metaEcoToComf(< EC :EcoController | Selected: false > CONF) = false .
  eq metaEcoToComf(< CC :ComfortController | Selected: true > CONF) = false .
  ceq metaEcoToComf(< Scheduler | Status: SC, TempAqChange: B, McChange: B1, ContrChange: B2 > CONF) = false
    if SC =/= MCChange .
  ceq metaEcoToComf(CONF) = false
    if cost(CONF) > 7/10 .
  eq metaEcoToComf(CONF) = true [owise] .

  *** Check if at least one rule is applicable
  op metaRuleApplicable : Configuration -> Bool .
  eq metaRuleApplicable(CONF) = metaComfToHErr(CONF) or metaComfToWhErr(CONF) or metaComfToWErr(CONF) or metaComfToEco(CONF)
                                or metaEcoToHErr(CONF) or metaEcoToWhErr(CONF) or metaEcoToWErr(CONF) or metaEcoToComf(CONF) .



*** TODO Should the metacontroller also turn the actuators off?
  ****************** Rules
  ****************************************** From comfort controller to error controller
  *** If the comfort controller is active and the heater is broken, then activate the heater error controller
  rl [MetaContrComfortToHErr] :
    < H :Heater | Broken: yes, A >
    < CC :ComfortController | Selected: true >
    < HEC :HeaterErrController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < H :Heater | Broken: yes, A >
    < CC :ComfortController | Selected: false >
    < HEC :HeaterErrController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, HErr) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrComfortToHErr]"] .

  *** If the comfort controller is active and the water heater is broken, then activate the water heater error controller
  rl [MetaContrComfortToWhErr] :
    < WH :Waterheater | Broken: yes, A >
    < CC :ComfortController | Selected: true >
    < WHEC :WaterheaterErrController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < WH :Waterheater | Broken: yes, A >
    < CC :ComfortController | Selected: false >
    < WHEC :WaterheaterErrController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, WhErr) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrComfortToWhErr]"] .

  *** If the comfort controller is active and the window is broken, then activate the window error controller
  rl [MetaContrComfortToWErr] :
    < W :Window | Broken: yes, A >
    < CC :ComfortController | Selected: true >
    < WEC :WindowErrController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < W :Window | Broken: yes, A >
    < CC :ComfortController | Selected: false >
    < WEC :WindowErrController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, WErr) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrComfortToWErr]"] .



  ******************************************* From eco controller to error controller
  *** If the eco controller is active and the heater is broken, then activate the heater error controller
  rl [MetaContrEcoToHErr] :
    < H :Heater | Broken: yes, A >
    < EC :EcoController | Selected: true >
    < HEC :HeaterErrController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < H :Heater | Broken: yes, A >
    < EC :EcoController | Selected: false >
    < HEC :HeaterErrController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, HErr) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrEcoToHErr]"] .

  *** If the eco controller is active and the water heater is broken, then activate the water heater error controller
  rl [MetaContrEcoToWhErr] :
    < WH :Waterheater | Broken: yes, A >
    < EC :EcoController | Selected: true >
    < WHEC :WaterheaterErrController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < WH :Waterheater | Broken: yes, A >
    < EC :EcoController | Selected: false >
    < WHEC :WaterheaterErrController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, WhErr) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrEcoToWhErr]"] .

  *** If the eco controller is active and the window is broken, then activate the window error controller
  rl [MetaContrEcoToWErr] :
    < W :Window | Broken: yes, A >
    < EC :EcoController | Selected: true >
    < WEC :WindowErrController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    =>
    < W :Window | Broken: yes, A >
    < EC :EcoController | Selected: false >
    < WEC :WindowErrController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, WErr) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    [print "rule: [MetaContrEcoToWErr]"] .




  ************************************** Change between comfort and eco controller
  *** If QA comfort is below 0.6 and eco controller is active, then change to comfort controller
  crl [MetaContrEcoToComf] :
    {< CC :ComfortController | Selected: false >
    < EC :EcoController | Selected: true >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    CONF}
    =>
    {< CC :ComfortController | Selected: true >
    < EC :EcoController | Selected: false >
    < MC :Metacontroller | MetaLog: ML (TS, Comf) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    CONF}
    if cost(CONF) <= 7/10
    [print "rule: [MetaContrEcoToComf]"] .

  *** If QA air quality is below 0.6 and comfort controller is active, then change to eco controller
  crl [MetaContrComfToEco] :
    {< CC :ComfortController | Selected: true >
    < EC :EcoController | Selected: false >
    < MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 >
    CONF}
    =>
    {< CC :ComfortController | Selected: false >
    < EC :EcoController | Selected: true >
    < MC :Metacontroller | MetaLog: ML (TS, Eco) >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 >
    CONF}
    if cost(CONF) > 7/10
    [print "rule: [MetaContrComfToEco]"] .



  *************************** Dummy rules
  *** If the metacontroller no other rule can be applied, this will be applied
  crl [MetacontrollerDoesNothing] :
    {< MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 > CONF}
    =>
    {< MC :Metacontroller | MetaLog: ML >
    < C :Clock | Timesteps: TS, Time: TI, TempLog: TL, AqLog: AL >
    < Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 > CONF}
    if metaRuleApplicable(< MC :Metacontroller | MetaLog: ML > < Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 > CONF) == false /\ R := cost(CONF)
    [print "rule: [MetacontrollerDoesNothing], Cost = " R] .

  *** If there is no metacontroller, this rules ensures that we can run tests anyway
  crl [noMetacontroller] :
    {< Scheduler | Status: MCChange, TempAqChange: B, McChange: false, ContrChange: B1 > CONF}
    =>
    {< Scheduler | Status: MCChange, TempAqChange: B, McChange: true, ContrChange: B1 > CONF}
    if noMetacontr(CONF) == true
    [print "rule: [noMetacontr]"] .

endm
eof
